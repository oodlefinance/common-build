apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'distribution'

group = projectGroup
version = version ?: '1.0.0'
archivesBaseName = projectName
sourceCompatibility = 1.11
jar.baseName = projectName

def jenkinsUrl = 'http://54.154.146.14:8080/'

def artifactory = System.getenv('JENKINS_URL') == jenkinsUrl ? 'http://artifactory.internal.carsnip.com:8081' : 'http://carsnip-jenkins.oodlefinance.com:8081'


dependencies {
  resolutionRules 'com.netflix.nebula:gradle-resolution-rules:latest.release'
}

sourceSets {
  acceptanceTest {
    groovy {
      srcDir file('/src/acceptanceTest/groovy')
      compileClasspath += main.output + test.output + configurations.testRuntime
      runtimeClasspath += output + compileClasspath
    }
  }

  integrationTest {
    groovy {
      srcDir file('/src/integrationTest/groovy')
      compileClasspath += main.output + test.output + configurations.testRuntime
      runtimeClasspath += output + compileClasspath
    }
  }
}

distributions {
  main {
    contents {
      from configurations.runtime.allArtifacts.files
      from configurations.runtime
    }
  }
}

repositories {
  // use our artifactory repository that proxies the other repositories such as maven central
  ivy {
    url "${artifactory}/artifactory/libs-release-local"
    layout "maven"
  }
  maven { url "${artifactory}/artifactory/libs-release" }
  jcenter()
}

configurations {
  compile {
    exclude group: "org.springframework", module: "spring-jcl"
  }
  all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 5, 'minutes'
  }
  acceptanceTestCompile.extendsFrom testImplementation
  acceptanceTestRuntime.extendsFrom testRuntime
  integrationTestCompile.extendsFrom testImplementation
  integrationTestRuntime.extendsFrom testRuntime
}

task acceptanceTest(type: Test) {
  testClassesDirs = sourceSets.acceptanceTest.output.classesDirs
  classpath = sourceSets.acceptanceTest.runtimeClasspath
  include '**/*'
  outputs.upToDateWhen { false }
}

task integrationTest(type: Test) {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  include '**/*'
  outputs.upToDateWhen { false }
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'snapshot', 'final'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.?\d-]?.*/
      }

      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

gradleLint {
  //unused-dependency covers used but undeclared classes as well as unused, so ignore any of the "one or more classes
  //in <dependency> are required by your code directly (no auto-fix available)", no easy fix :(
  rules = ['all-dependency', 'unused-dependency', 'unused-exclude-by-dep']
  excludedRules = ['unused-exclude-by-conf']

  //Can be commented out after the first time it's run as it's useful, but it also doesn't recognise implementation, and
  //replaces with compile (and sets hard versions.) If they fix this then can be added to excludedRules
  //excludedRules += 'undeclared-dependency'

  alwaysRun = false
}
