import java.text.SimpleDateFormat

println 'Project using lp-service-common script as of ' + new SimpleDateFormat("yyyy-MM-dd HH:mm:ss").format(new Date())

buildscript {
    ext {
        devopsGradleDir = "${rootDir}/oodle-devops/gradle"
        springBootVersion = '2.7.5' // Version used for gradle plugin AND framework dependencies
        // Plugin versions
        dependencyCheckVersion = '7.3.0'
        versionsPluginVersion = '0.43.0'
        // Versions for dependencies included in this script
        lombokVersion = '1.18.24'
        logstashEncoderVersion = '7.2'
        // Versions for optional dependencies that may be used in project build.gradle files
        // Note these can be overridden in build.gradle in an ext {} block
        oodleCommonsVersion = '0.2.2'
        oodleCloudVersion = '2.3.1'
        oodleJwtVersion = '1.2.1'
        oodleMetricsVersion = '1.3.1'
        oodleLoggingVersion = '1.0.4'
        libBffObjectsVersion = 'v1.3.0'
        // Transitive dependency versions forced temporarily to resolve security issues
        snakeYamlVersion = '1.33'
        woodstoxCoreVersion = '6.4.0'
    }
    // Repository block for sourcing plugins
    repositories {
        maven {
            url "https://${codeartifactDomain}-${awsBuildAccountId}.d.codeartifact.eu-west-1.amazonaws.com/maven/oodle-maven-repo/"
            credentials {
                username "${codeartifactUser}"
                password "${codeartifactToken}"
            }
        }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.owasp:dependency-check-gradle:${dependencyCheckVersion}"
        classpath "com.github.ben-manes:gradle-versions-plugin:${versionsPluginVersion}"
    }
}

apply plugin: 'java'
// In order to provide plugins for other projects sourcing this script, we must specify the plugins by type
// See https://blog.mrhaki.com/2015/10/gradle-goodness-apply-external-script.html

apply plugin: org.springframework.boot.gradle.plugin.SpringBootPlugin
apply plugin: org.owasp.dependencycheck.gradle.DependencyCheckPlugin
apply plugin: com.github.benmanes.gradle.versions.VersionsPlugin

// Repository block for sourcing dependencies
repositories {
    maven {
        url "https://${codeartifactDomain}-${awsBuildAccountId}.d.codeartifact.eu-west-1.amazonaws.com/maven/oodle-maven-repo/"
        credentials {
            username "${codeartifactUser}"
            password "${codeartifactToken}"
        }
    }
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "net.logstash.logback:logstash-logback-encoder:${logstashEncoderVersion}"
    implementation "com.oodlefinance:logging:${oodleLoggingVersion}"

    // Constraints on transitive dependencies. Include a note for when these can be removed.
    constraints {
        implementation("org.yaml:snakeyaml:${snakeYamlVersion}") {
            // Transitive dependency of spring boot, should be resolved in version 3
            because 'Version < 1.33 has vulnerabilities reported by the OWASP dependency checker'
        }
    }
}

// Add additional tasks from oodle-devops repo, as used in jenkinsfile:
def publishArtifacts = new File("${devopsGradleDir}/publishArtifacts.gradle")
def qualityChecks = new File("${devopsGradleDir}/qualityChecks.gradle")
def sonarQube = new File("${devopsGradleDir}/sonarQube.gradle")

if (publishArtifacts.exists()) apply from: publishArtifacts
if (qualityChecks.exists()) apply from: qualityChecks
if (sonarQube.exists()) apply from: sonarQube


build {
    // Before each build, run dependencyUpdates from gradle-versions-plugin
    // This gives a convenient log of all dependency versions used in a build
    dependsOn(tasks.getByName('dependencyUpdates'))
}

sourceCompatibility = JavaVersion.valueOf(javaVersion)

// Use a timestamp for a version number
project.version = new SimpleDateFormat("'v'yyyyMMddHHmmss").format(new Date())


bootJar {
    archiveFileName = "${rootProject.name.toLowerCase()}.jar"
    manifest {
        attributes(
                'Implementation-Version': project.version,
                'Bundle-SymbolicName': project.group,
                'Git-commit-hash': "git rev-parse --verify HEAD".execute().text.trim()
        )
    }
}
